program Edit
{
    #define JSON_EXPRESS // .code and .json are generated by us so assume .json files have no errors
    #define SHORT_CALLS  // use CALLB to save space
    
    uses "/Source/System/System"
    uses "/Source/System/Screen"
    uses "/Source/System/Keyboard"
    
    uses "Panel"
    uses "MenuBar"
    uses "StatusBar"
    uses "Editor"
    uses "Commands"
    
    uses "/Source/Compiler/Tokens/Parser" // SetInteractive
    
    string optionsPath;
    string OptionsPath { get { return optionsPath; } }    
    uint   comPort;
    uint   COMPort { get { return comPort; } }
    Hopper()
    {
        <string> rawArgs = System.Arguments;
        <string> args;
        
        string filePath;
        bool showHelp;
        comPort = 4242; // bogus comPort value
        
        Editor.Locate(0, 0, Screen.Columns, Screen.Rows);
        Parser.SetInteractive(Editor.Left+1, Editor.Top + Editor.Height-1);
        
        for (uint iArg = 0; iArg < rawArgs.Count; iArg++)
        {
          string arg = rawArgs[iArg];
          if ((arg.Length == 2) && (arg[0] == '-'))
          {
              arg = arg.ToLower();
              switch (arg)
              {
                  case "-p":
                  {
                      iArg++;
                      if ((iArg == rawArgs.Count) || !UInt.TryParse(rawArgs[iArg], ref comPort))
                      {
                          args.Clear();
                          break;
                      }
                  }
                  default:
                  {
                      args.Clear();
                      break;
                  }
              }
          }
          else
          {
              args.Append(arg);
          }
        }
        if (args.Count > 1)
        {
            showHelp = true;
        }
        else if (args.Count == 1)
        {
            filePath = args[0];
        }
        if (filePath.Length == 0)
        {
            showHelp = true;
        }

        loop
        {
            bool doNew = false;
            if (!showHelp)
            {
                loop
                {
                    // check the file
                    string fullPath;
                    if (File.Exists(filePath))
                    {
                        break;
                    }
                    fullPath = Path.Combine(System.CurrentDirectory, filePath);
                    if (File.Exists(fullPath))
                    {
                        filePath = fullPath;
                        break;
                    }
                    string extension = Path.GetExtension(filePath);
                    if (extension == ".")
                    {
                        string filePathExt = filePath + ".hs";
                        if (File.Exists(filePathExt))
                        {
                            filePath = filePathExt;
                            break;
                        }
                        string fullPathExt = Path.Combine(System.CurrentDirectory, filePathExt);
                        if (File.Exists(fullPathExt))
                        {
                            filePath = fullPathExt;
                            break;
                        }
                        filePathExt = filePath + ".asm";
                        if (File.Exists(filePathExt))
                        {
                            filePath = filePathExt;
                            break;
                        }
                        fullPathExt = Path.Combine(System.CurrentDirectory, filePathExt);
                        if (File.Exists(fullPathExt))
                        {
                            filePath = fullPathExt;
                            break;
                        }
                        filePathExt = filePath + ".tc";
                        if (File.Exists(filePathExt))
                        {
                            filePath = filePathExt;
                            break;
                        }
                        fullPathExt = Path.Combine(System.CurrentDirectory, filePathExt);
                        if (File.Exists(fullPathExt))
                        {
                            filePath = fullPathExt;
                            break;
                        }
                    }
                    if (!File.Exists(fullPath))
                    {
                        if (!filePath.Contains('/'))
                        {
                            filePath = fullPath;
                        }
                        doNew = true;
                    }
                    break;
                } // loop
            }
            if (showHelp)
            {
                PrintLn("EDIT <filepath> [-p <comPort>]");
                break;
            }
            
            filePath = Path.GetCorrectCase(filePath); // get correct case
            
            string lowerFilePath = filePath.ToLower();
            if (!lowerFilePath.EndsWith(".options") && !lowerFilePath.EndsWith(".local"))
            {
                optionsPath = Path.MakeOptions(filePath);
            }
            
            Screen.Clear();
            Commands.Initialize();
           
            <string, variant> menubar   = MenuBar.New(); 
            <string, variant> statusbar = StatusBar.New(); 
            
            Editor.New(statusbar, menubar); 

            if (!doNew)
            {            
                Editor.LoadFile(filePath);
            }
            
            MenuBar.Draw(menubar);
            StatusBar.Draw(statusbar);
            Editor.Draw();

            if (doNew)
            {
                Editor.SetNewPath(filePath);
                Commands.Execute("New");
                if (!File.Exists(Editor.CurrentPath))
                {
                    ExitCommand.Execute();
                    break;
                }
            }
            
            loop
            {
                Key key = ReadKey();
               
                // offer the key to the menus
                if (MenuBar.OnKey(menubar, key))
                {
                    // consumed by menus
                }
                else if (StatusBar.OnKey(statusbar, key))
                {
                    // consumed by status bar
                }
                else
                {
                    string commandName = Commands.KeyToCommand(key);
                    if (commandName.Length != 0) // checks IsEnabled too
                    {
                        Commands.Execute(commandName);
                    }
                    else if (Editor.OnKey(key)) // to Editor last since it is greedy about eating keys
                    {
                        // consumed by editor
                    }
                }
                if (ExitCommand.IsExiting())
                {
                    break;
                }
            } // loop
            break;
        } // loop
    }
}
